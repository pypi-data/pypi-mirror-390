WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ "//" ~ (!"\n" ~ ANY)* }

select = { "SELECT" }
from = { "FROM" }

quantity_single = @{ ASCII_DIGIT+ }
quantity_many = ${ quantity_single ~ "-" ~ quantity_single }
quantity = { quantity_many | quantity_single }

class = @{ "CLASS" | "class" }
placement = @{ "PLACEMENT" | "placement" }
tag = @{ "TAG" | "tag" }
range = @{ "RANGE" | "range" }
bad_query = @{ (ASCII_ALPHANUMERIC | "_")+ }

query_name = ${ (class | placement | tag | range) ~ ! (ASCII_ALPHANUMERIC | "_")
  | bad_query }

string = @{ "\"" ~ ( "\"\"" | (!("\"" | "\r" | "\n") ~ (ANY)) )* ~ "\"" }

department_id = { ASCII_ALPHA_UPPER{4} }
class_id = { ASCII_DIGIT{4} }

class_argument = { department_id ~ class_id }

query_argument = { string | class_argument }

lpar = _{ "(" }
rpar = _{ ")" }

XYZ = { query_name ~ lpar ~ (query_argument ~ ("," ~ query_argument)*) ~ rpar }

selector_list = { "[" ~ selector_single ~ ("," ~ selector_single)* ~ "]" }
selector_single = { statement | XYZ }

selector = { selector_list | selector_single }

semicolon = _{ ";" }

statement = !{ select ~ quantity ~ from ~ selector }

sep = _{ ":"}

special_statement = { statement ~ sep ~ string ~ (sep ~ quantity_single)? }

file = { SOI ~ (special_statement ~ semicolon)* ~ EOI  }