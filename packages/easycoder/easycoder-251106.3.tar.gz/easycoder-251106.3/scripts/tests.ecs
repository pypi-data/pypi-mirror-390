!	A test script

	script Tests

	variable Data
	variable D
	variable E
	variable M
	variable N
	variable Array
	variable X
	variable Y
	variable Z
	variable Dictionary
	file File

!	debug step

	log `FOR loop counting to 10`
	put 0 into N
	while N is less than 10
	begin
		log N
		increment N
	end

	log `Compare 10 integers for equality`
	put 0 into N
	while N is less than 10
	begin
		if N is 1234567890 begin end
		increment N
	end

	log `Allocate and initialize a 10 element array`
	put 0 into N
	while N is less than 10
	begin
		add 1 to N giving M
		set the elements of Array to M
		index Array to N
		put N into Array
		increment N
	end

	log `Allocate and initialize a 10 element JSON dictionary`
	put json `{}` into Dictionary
	put 0 into N
	while N is less than 10
	begin
		put 0 into M
		while M is less than 10
		begin
			set property N cat `,` cat M of Dictionary to M
			increment M
		end
		increment N
	end

	put 20 into Y
	put 6 into X

	add X to Y giving Z
	assert Z is 26
	add X to Y
	assert Y is 26

	take 5 from Y giving Z
	assert Z is 21
	take 5 from Y
	assert Y is 21

	multiply Y by X giving Z
	assert Z is 126
	multiply Y by X
	assert Y is 126

	divide Y by 3 giving Z
	assert Z is 42
	divide Y by 3
	assert Y is 42

	put Y modulo 10 into Z
	assert Z is 2

	put empty into X
	set property `name` of X to `Fred`
	assert property `name` of X is `Fred`
	put empty into Y
	put 1 into N
	while N is less than 6
	begin
		append N to Y
		increment N
	end
	assert element 2 of Y is 3

	set element 2 of Y to `Some data`
	assert element 2 of Y is `Some data`
	assert element 2 of Y is string
	assert N is numeric

	set Y
	assert Y is true
	clear Y
	assert Y is false
	toggle Y
	assert Y is true
	toggle Y
	assert Y is false

	set Y
	assert Y is boolean
	put 5 into Y
	assert Y is not boolean
	put `hello` into Y
	assert Y is not boolean

	put 0 into N
	while N is less than 10
	begin
		assert N is even
		add 2 to N
	end

	assert 52 is greater than 51
	assert 52 is less than 53
	assert 52 is not greater than 52
	assert 52 is not less than 52

	fork to Concurrent
	put 0 into N
Loop1:
	gosub to Print
	wait 10 ticks
	add 1 to N
	if N is less than 10 go to Loop1

	open File `test.txt` for writing
	write line `Hello, world!` to File
	close File
	open File `test.txt` for reading
	read X from File
	assert X is `Hello, world!`  cat newline
	close File
	open File `test.txt` for appending
	write `I'm back!` to File
	close File
	open File `test.txt` for reading
	read X from File
	close File
	delete file `test.txt`
	assert X is `Hello, world!`  cat newline cat `I'm back!`

    put now into N
    multiply N by 1000
    log N
    log datime N
    log datime 1735689600000 format `%b %d %Y %H:%M:%S`

    put `"Goodbye,\n cruel world!"` into D
    put encode D into E
    log E
    put decode E into D
    log D

    set the encoding to `base64`

    put `"Goodbye, cruel world!"` into D
    put encode D into E
    log E
    put decode E into D
    log D

    log from 8 to 12 of `This is some text`

    log hash `Graham`

    log the timestamp cat ` ` cat now

    log the files in `.`

    log the memory

    put json `[]` into Data
    put 0 into N
    while N is less than 10
    begin
        append N to Data
        increment N
    end

    log Data
    log the index of 5 in Data

	wait 2
	log `All done!`
	exit

Print:
	log `N = ` cat N
	return

Concurrent:
	put 0 into M
Loop2:
	log `         M = ` cat M
	wait 20 ticks
	add 1 to M
	if M is less than 10 go to Loop2
	stop
